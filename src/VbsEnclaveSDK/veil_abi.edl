// Copyright (c) Microsoft Corporation.
// Licensed under the MIT License.

enclave
{
    struct keyCredentialCacheConfig
    {
        uint32_t cacheOption;
        uint32_t cacheTimeoutInSeconds;
        uint32_t cacheUsageCount;
    };

    struct attestationReportAndSessionInfo
    {
        vector<uint8_t> attestationReport;
        uintptr_t sessionInfo;
    };

    struct credentialAndSessionInfo
    {
        uintptr_t credential;
        uintptr_t sessionInfo;
    };

    trusted
    {
        HRESULT taskpool_run_task(
            uint64_t taskpool_instance_vtl1,
            uint64_t task_id
        );

        // hello user bound key
        attestationReportAndSessionInfo userboundkey_get_attestation_report(
            vector<uint8_t> challenge);

        // Cleanup function for session handles from VTL0
        void userboundkey_close_session(
            uintptr_t session_info
        );
    };

    untrusted
    {
        // infra
        HRESULT internal_printf(string str);
        HRESULT internal_wprintf(wstring str);

        // taskpool
        HRESULT taskpool_make(
            uintptr_t enclave,
            uint64_t taskpool_instance_vtl1,
            uint32_t thread_count,
            bool must_finish_all_queued_tasks,
            [out] uintptr_t taskpool_instance_vtl0
        );

        HRESULT taskpool_delete(
            uintptr_t taskpool_instance_vtl0
        );

        HRESULT taskpool_schedule_task(
            uintptr_t taskpool_instance_vtl0,
            uint64_t task_id
        );
        
        HRESULT taskpool_cancel_queued_tasks(
            uintptr_t taskpool_instance_vtl0
        );

        // logger
        HRESULT add_log(
            wstring log,
            wstring log_file_path
        );

        // hello user bound key
        credentialAndSessionInfo userboundkey_establish_session_for_create(
            uintptr_t enclave,
            wstring key_name,
            uintptr_t ecdh_protocol,
            wstring message,
            uintptr_t window_id,
            keyCredentialCacheConfig cache_config,
            uint32_t key_credential_creation_option
        );

        credentialAndSessionInfo userboundkey_establish_session_for_load(
            uintptr_t enclave,
            wstring key_name,
            wstring message,
            uintptr_t window_id
        );

        vector<uint8_t> userboundkey_get_authorization_context_from_credential(
            uintptr_t credential,
            vector<uint8_t> encrypted_kcm_request_for_get_authorization_context,
            wstring message,
            uintptr_t window_id
        );

        vector<uint8_t> userboundkey_get_secret_from_credential(
            uintptr_t credential,
            vector<uint8_t> encrypted_kcm_request_for_derive_shared_secret,
            wstring message,
            uintptr_t window_id
        );

        // Format a key name with SID information for use with Windows Hello
        wstring userboundkey_format_key_name(
            wstring key_name
        );

        // VTL0 function to safely delete/release a credential
        void userboundkey_delete_credential(
            uintptr_t credential
     );
    };
};
